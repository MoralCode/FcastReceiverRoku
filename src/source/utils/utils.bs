
' https://developer.roku.com/en-ca/docs/references/scenegraph/media-playback-nodes/video.md#playback-fields
enum VideoControl
	none = "none" ' No play state set
	play = "play" ' Start video play
	stop = "stop" ' stop video play
	pause = "pause" ' Pause video play
	resume = "resume" ' Resume video play after a pause
	replay = "replay" ' Replay video
	prebuffer = "prebuffer" ' Starts buffering the video stream before the Video node actually begins playback. Only one video stream can be buffering in the application at any time.Setting the control field to prebuffer for another video stream after setting prebuffer for a previous video stream stops the buffering of the previous video stream.
	skipcontent = "skipcontent" ' Skip the currently - playing content and begin playing the next content in the playlist.if the content is not a playlist, or if the current content is the end of the playlist, this will end playback.
end enum

namespace utils
	function ByteArrayToHex(bytes as object) as string
		hexString = ""
		for each byte in bytes
			' Convert byte to hex (0-255)
			hex = Right("0" + StrI(byte, 16), 2)
			hexString += hex + " "
		end for
		return hexString
	end function
end namespace


function GetDurationStringStandard(totalSeconds = 0 as integer, pad = True as boolean, showEmptyHours = false as boolean) as string
	remaining = totalSeconds
	hours = Int(remaining / 3600).ToStr()
	remaining = remaining mod 3600
	minutes = Int(remaining / 60).ToStr()
	remaining = remaining mod 60
	seconds = remaining.ToStr()

	if hours <> "0" or showEmptyHours then
		return IIf(pad, PadLeft(hours, "0", 2), hours) + ":" + PadLeft(minutes, "0", 2) + ":" + PadLeft(seconds, "0", 2)
	else
		return IIf(pad, PadLeft(minutes, "0", 2), minutes) + ":" + PadLeft(seconds, "0", 2)
	end if
end function

function IIf(condition as boolean, result1 as dynamic, result2 as dynamic) as dynamic
	if condition then
		return result1
	else
		return result2
	end if
end function

function PadLeft(value as string, padChar as string, totalLength as integer) as string
	while value.Len() < totalLength
		value = padChar + value
	end while
	return value
end function
